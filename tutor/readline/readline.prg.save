
/*
 *  CCC - The Clipper to C++ Compiler
 *  Copyright (C) 2005 ComFirm BT.
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public
 *  License along with this library; if not, write to the Free Software
 *  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */

//readline osztály
//soronként olvas egy file descriptorból
//osztálydefiníció class utasítással


****************************************************************************
class readline(object)
    method initialize
    attrib fd
    attrib buffer
    method readline

****************************************************************************
static function readline.initialize(this,fd) 
    this:(object)initialize
    this:fd:=fd
    this:buffer:=a""
    return this
    
****************************************************************************
static function readline.readline(this)

local line,nlpos
local x:=replicate(x"00",1024),nbyte
static nl:=bin(10)

    while( 0==(nlpos:=at(nl,this:buffer)) )
        nbyte:=fread(this:fd,@x,len(x))
        if( nbyte>0 )
            this:buffer+=left(x,nbyte)
        else
            exit
        end
    end
    
    if( nlpos>0 )
        line:=left(this:buffer,nlpos)
        this:buffer:=substr(this:buffer,nlpos+1)
    else
        line:=if(len(this:buffer)==0,NIL,this:buffer)
        this:buffer:=a""
    end
    return line  //a következő sor, vagy NIL, ha vége a filének

****************************************************************************

